plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.beryx.jlink' version '3.0.1'
}

group 'fr.elpine'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.postgresql:postgresql:42.2.27'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'com.opencsv:opencsv:5.9'
}

compileJava.options.encoding = 'UTF-8'

application {
    mainModule = 'fr.elpine.astre'
    mainClass = 'fr.elpine.astre.Controleur'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml']
}

jar {
    manifest {
        attributes 'Implementation-Title': "astre", 'Main-Class': application.mainClass
    }
}

jlink {
    imageZip = project.file("${buildDir}/distributions/astre-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher{
        name = 'Astre'
        noConsole = true
        jvmArgs = ['-Dlogback.configurationFile=./logback.xml']
    }
    jpackage {
        skipInstaller = false

        installerOptions += [
                '--description', 'Astre Project',
                '--app-version', '1.0',
                '--copyright', 'Copyrigtht 2024 El Pine',
                '--license-file', 'LICENSE',
                '--vendor', 'El Pine'
        ]

        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions     += ['--icon', 'src/main/resources/fr/elpine/astre/ihm/icon.ico']
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }

        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            imageOptions     += ['--icon', 'src/main/resources/fr/elpine/astre/ihm/icon.png']
            installerOptions += ['--linux-menu-group', 'Office', '--linux-shortcut']
        }

        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            imageOptions += ['--icon', 'src/main/resources/fr/elpine/astre/ihm/icon.icns']
        }
    }
}